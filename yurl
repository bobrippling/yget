#!/bin/sh

[ $# -eq 1 ] || { echo "usage: $0 URL" >/dev/stderr; exit 1; }
URL='http://www.youtube.com/'$(echo "$1"|sed '
	s|[&!?=]|/|g
	s|^...........$|/v/&|
	s|.*\.be/|/v/|
	s|.*/v/\([^/]*\).*|watch?v=\1|
	s|.*/p/\([^/]*\).*|view_play_list?p=\1|')
case "$URL" in
*\?p=* )
	# playlist url
	p=1
	wget "$URL" -q -O- |awk '/data-video-ids/{
		sub(/.*data-video-ids="/, "")
		sub(/".*/, "")
		n = split($0, a, ",")
		for (i = 1; i <= n; i++)
			printf "%03d http://www.youtube.com/watch?v=%s\n", i, a[i]
		exit
	}' ;;
* )
	# video url
	wget "$URL" -q -O- |awk '
BEGIN {
	for (i = 0; i < 256; i++)
		tab[sprintf("%X", i)] = sprintf("%c", i)
}

function unquote(s) {
	n = split(s, t, "%")
	r = t[1]
	for (i = 2; i <= n; i++) {
		x = substr(t[i], 1, 2)
		if (x in tab)
			r = r tab[x] substr(t[i], 3)
		else
			r = r "%" t[i]
	}
	return r
}
function strip() {
	sub(/^[^:]*: '\''?/, "")
	sub(/'\''?,$/, "")
}
function getmeta() {
	sub(/^.*content="/, "")
	sub(/".*$/, "")
	gsub(/&quot;/, "\"")
	gsub(/&amp;/, "\\&")
}
/<meta name="title"/ {
	getmeta()
	c["title"] = $0
}
/<meta name="description"/ {
	getmeta()
	c["desc"] = $0
}
/<meta name="keywords"/ {
	getmeta()
	c["tags"] = $0
}
/VIDEO_TITLE/ {
	strip()
	c["title"] = $0
}
/IS_WIDESCREEN/ {
	strip()
	c["wide"] = $0
}
/IS_HD_AVAILABLE/ {
	strip()
	c["hd"] = $0
}
/verify-age/ {
	unsafe=1
}
function getvars() {
	for (i in a) {
		split(a[i], b, /[:=] ?/)
		c[b[1]] = unquote(b[2])
	}
	# fix keys
	c["id"] = c["video_id"]
	c["length"] = c["length_seconds"]
#	c["tags"] = c["keywords"]
	c["fmt"] = c["fmt_map"]
	gsub(/\/.\/.\/[^,\/]*/, "", c["fmt"])
	gsub(/=/, "%3D", c["t"])
#	c["url"] = "http://www.youtube.com/get_video?video_id=" c["video_id"] "&t=" c["t"]
	c["thumb"] = "http://i.ytimg.com/vi/" c["video_id"] "/default.jpg"
	c["thumbhq"] = "http://i.ytimg.com/vi/" c["video_id"] "/hqdefault.jpg"

	n = split(c["fmt_url_map"], u, ",")
	for (i = 1; i <= n; i++) {
		split(u[i], ui, "|")
		fmt = fmt " url" ui[1]
		c["url" ui[1]] = ui[2]
	}
	ok = 1
}
/name=\\"flashvars\\"/ {
	sub(/^.*flashvars\\" value=\\"/, "")
	sub(/\\".*$/, "")
	split($0, a, /&/)
	getvars()
}
/SWF_ARGS/ {
	sub(/^.*{/, "")
	sub(/}.*$/, "")
	gsub(/"/, "")
	split($0, a, /, /)
	getvars()
}
#/var swfConfig = {/
#/PLAYER_CONFIG/
END {
	if (!ok) {
		if (unsafe)
			print "err\t(verify your age by signing in)"
		exit 1
	}
	N = split("fmt" fmt " id t length title desc tags wide hd thumb thumbhq", a)
	for (i = 1; i <= N; i++)
		print a[i] "\t" c[a[i]]
	exit 0
}'
esac
