#!/bin/sh

[ $# -eq 1 ] || { echo "usage: $0 URL" 1>&2; exit 1; }
V=$(echo "$1"|sed '
	s|[^a-zA-Z0-9_-]|/|g
	s|^...........$|/v/&|
	s|/embed/|/v/|
	s|youtu/be/|/v/|
	s|/p/u/[0-9]*/|/v/|
	s|.*/v/\([^/]*\).*|\1|
	s|.*/v/.*||
	s|^.............*||')
P=$(echo "$1"|sed '
	s|[^a-zA-Z0-9_-]|/|g
	s|^P*L*\(................\)$|/p/\1|
	s|/list/P*L*\(................\)$|/p/\1|
	s|.*/p/\([^/]*\).*|\1|
	s|.*/p/.*||
	s|^...........$||')
VURL="http://www.youtube.com/watch?v=$V"
PURL="http://www.youtube.com/view_play_list?p=$P"
[ "$P" ] || [ "$V" ] || { echo 'no video id or playlist id found' 1>&2; exit 1; }
[ "$V" ] || {
	# playlist url
	p=1
	wget "$PURL" -q -O- |awk '/data-video-ids/{
		sub(/.*data-video-ids="/, "")
		sub(/".*/, "")
		n++
		printf "%03d http://www.youtube.com/watch?v=%s\n", n, $0
	}'
	exit 0
}
[ "$V" ] && {
	# video url
	wget "$VURL" -q -O- |awk -v pl=$P '
BEGIN {
	for (i = 0; i < 256; i++)
		tab[sprintf("%X", i)] = sprintf("%c", i)
}
function unquote(s, i,n,x,t) {
	n = split(s, t, "%")
	s = t[1]
	for (i = 2; i <= n; i++) {
		x = substr(t[i], 1, 2)
		if (x in tab)
			s = s tab[x] substr(t[i], 3)
		else
			s = s "%" t[i]
	}
	return s
}
function unquote2(s, i,n,x,t) {
	n = split(s, t, "&#")
	s = t[1]
	for (i = 2; i <= n; i++) {
		x = substr(t[i], 1, 2) + 0
		if (x > 9 && x < 100)
			s = s sprintf("%c", x) substr(t[i], 4)
		else
			s = s "&#" t[i]
	}
	return s
}
function strip() {
	sub(/^[^:]*: '\''?/, "")
	sub(/'\''?,$/, "")
}
function getmeta() {
	sub(/^.*content="/, "")
	sub(/".*$/, "")
	gsub(/&quot;/, "\"")
	gsub(/&amp;/, "\\&")
	$0 = unquote2($0)
}
/<meta name="title"/ {
	getmeta()
	c["title"] = $0
}
/<meta name="description"/ {
	getmeta()
	c["desc"] = $0
}
/<meta name="keywords"/ {
	getmeta()
	c["keywords2"] = $0
}
/IS_WIDESCREEN/ {
	strip()
	c["wide"] = $0
}
/IS_HD_AVAILABLE/ {
	strip()
	c["hd"] = $0
}
/verify-age/ {
	unsafe=1
}
function getvars() {
	for (i in a) {
		split(a[i], b, /[:=] ?/)
		sub(/^"/,"",b[1])
		sub(/"$/,"",b[1])
		sub(/^"/,"",b[2])
		sub(/"$/,"",b[2])
		c[b[1]] = unquote(b[2])
#		print b[1],c[b[1]]
	}
	# fix keys
	c["id"] = c["video_id"]
	c["length"] = c["length_seconds"]
#	c["tags"] = c["keywords2"]
	c["tags"] = c["keywords"]
#	gsub(/=/, "%3D", c["t"])
#	c["url"] = "http://www.youtube.com/get_video?video_id=" c["video_id"] "&t=" c["t"]
	c["thumb"] = "http://i.ytimg.com/vi/" c["video_id"] "/default.jpg"
	c["thumbhq"] = "http://i.ytimg.com/vi/" c["video_id"] "/hqdefault.jpg"
	c["list"] = pl

	n = split(c["url_encoded_fmt_stream_map"], u, ",")
	for (i = 1; i <= n; i++) {
		k = split(u[i],a,/&/)
		for (j in a) {
			split(a[j], b, /=/)
			sub(/"/, "", b[2])
			d[b[1]] = unquote(b[2])
#			print i,b[1],d[b[1]]
		}
		it = d["itag"]
		c["url" it] = d["url"]
		c["fmt" it] = d["type"]
		urls = urls " fmt" it " url" it
		c["fmts"] = c["fmts"] it " "
	}
	n = split(c["fmt_list"], u, ",")
	for (i = 1; i <= n; i++) {
		k = split(u[i],a,/\//)
		c["fmt" a[1]] = a[2] " " c["fmt" a[1]]
	}
	ok = 1
}
/flashvars="/ {
	sub(/^.*flashvars="/, "")
	sub(/".*$/, "")
	split($0, a, /&amp;/)
	getvars()
}
/SWF_ARGS/ {
	sub(/^.*{/, "")
	sub(/}.*$/, "")
	gsub(/"/, "")
	split($0, a, /, /)
	getvars()
}
#/var swfConfig = {/
#/PLAYER_CONFIG.: {/ {
#	sub(/.*PLAYER_CONFIG.: {/, "")
#	sub(/}.*$/, "")
#	sub(/\\/,"")
#	split($0, a, /, /)
#	getvars()
#}
END {
	if (!ok) {
		if (unsafe)
			print "err\t(verify your age by signing in)"
		exit 1
	}
#	N = split("fmt fmt_list " urls " id t length title desc tags wide hd thumb thumbhq list", a)
	N = split("fmts " urls " id length title desc tags thumbhq list", a)
	for (i = 1; i <= N; i++)
		print a[i] "\t" c[a[i]]
}'
}
